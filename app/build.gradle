buildscript {
    repositories {
        google()
        mavenCentral()
    }
    dependencies {
        classpath 'com.google.gms:google-services:4.3.10' // google-services plugin
    }
}

apply plugin: 'com.android.application'

android {
    compileSdkVersion rootProject.ext.compileSdkVersion

    defaultConfig {
        applicationId "com.voxeet.sdk.sample"
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion
        multiDexEnabled true
        versionCode 1
        versionName "1.0"

        buildConfigField "String", "CONSUMER_KEY", "\"" + project.properties["PROD_CONSUMER_KEY"]+"\""
        buildConfigField "String", "CONSUMER_SECRET", "\"" + project.properties["PROD_CONSUMER_SECRET"]+"\""

        buildConfigField "String", "USE_SDK_OAUTH_URL", "\"" + project.properties["USE_SDK_OAUTH_URL"]+"\""
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    dexOptions {
        javaMaxHeapSize "4g"
    }

    packagingOptions {
        exclude 'META-INF/ASL2.0'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/NOTICE.txt'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/MANIFEST.MF'
    }

    lintOptions {
        abortOnError false
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

dependencies {
    implementation project(':uxkit')
    implementation project(':uxkit-youtube')
    implementation project(':uxkit-exoplayer-support')
    implementation project(':uxkit-firebase')

    // to add the default service
    implementation project(':uxkit-system-service')

    testImplementation libs.junit

    //also used as annotation processor
    implementation libs.butterknife
    annotationProcessor libs.butterknife

    implementation libs.androidx.material
    implementation libs.androidx.multidex

    //when working with our oauth example
    //this converter is here to convert "string" only result
    //if a regular json is sent, please use Jackson
    implementation libs.retrofit.converter.scalars
}

//add google-services.json to enable google-services plugin
if (file("./google-services.json").exists()) {
    apply plugin: 'com.google.gms.google-services'
}

apply from: "../gradle/license.gradle"