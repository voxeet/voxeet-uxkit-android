buildscript {
    repositories {
        google()
        mavenCentral()
        maven { url 'https://oss.sonatype.org/content/repositories/snapshots' }
        maven { url 'https://plugins.gradle.org/m2' }
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:7.2.2'

        classpath 'com.jaredsburrows:gradle-license-plugin:0.8.91-SNAPSHOT'
        classpath "io.github.gradle-nexus:publish-plugin:1.1.0"
    }
}

apply plugin: "io.github.gradle-nexus.publish-plugin"

def modules_sdk = [
        // main module
        'uxkit',
        // firebase support
        'uxkit-firebase',
        // in conference specific "presentation"
        'uxkit-exoplayer-support', 'uxkit-youtube', 'uxkit-screenshare',
        // plugins related to services and system info
        'uxkit-incoming-call', 'uxkit-self-managed-call', 'uxkit-system-service',
        // common module
        'uxkit-common',

]

ext {
    buildToolsVersion = "32.0.0"
    minSdkVersion = 21
    compileSdkVersion = 32
    targetSdkVersion = 32

    groupId="io.dolby"
    androidXCore = "1.6.0"
    androidXAppCompatVersion = "1.4.0"
    androidXRecyclerVersion = "1.2.1"

    //underlying sdk
    //voxeetSdkVersion defined in the sdk.gradle file
    //voxeetUXKitVersion defined in the uxkit.gradle file
}

apply from: "./uxkit.gradle"
apply from: "./sdk.gradle"

group = ext.groupId

nexusPublishing {
    repositories {
        sonatype {
            // override default urls as default are oss.sonatype.org
            nexusUrl.set(uri("https://s01.oss.sonatype.org/service/local/"))
            snapshotRepositoryUrl.set(uri("https://s01.oss.sonatype.org/content/repositories/snapshots/"))
        }
    }
}

allprojects {
    repositories {
        mavenLocal()
        google()
        mavenCentral()
        maven {
            url "https://android-sdk.voxeet.com/release"
            content {
                includeGroup "com.voxeet.sdk"
                //any other possible group with a new includeGroup line but as far as I remember I only created and set one group
            }
        }

        maven {
            url "https://android-sdk.voxeet.com/beta"
            content {
                includeGroup "com.voxeet.sdk" //same as above
            }
        }
    }
}


task cleanAll {
    dependsOn = modules_sdk.collect { ":${it}:clean" }
}

task assembleAll {
    dependsOn = modules_sdk.collect { ":${it}:assembleRelease" }
}

task licenseAll {
    dependsOn = modules_sdk.collect { ":${it}:licenseReleaseReport" }

    modules_sdk.collect { "${it}/src/main/assets" }
            .each { folder -> new File(folder).mkdirs() }

    modules_sdk.each { name ->
        def report = "${name}/build/reports/licenses/licenseReleaseReport.json"
        def new_report = "${name}/build/reports/licenses/${name}.json"
        new File(report).renameTo(new File(new_report))

        copy { from new_report into "${name}/src/main/assets/" }
    }
}

task installAll {
    dependsOn = modules_sdk.collect { ":${it}:publishToMavenLocal" }
}

task uploadAll {
    dependsOn = modules_sdk.collect { ":${it}:publish" }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}

subprojects {
    task allDeps(type: DependencyReportTask) {}
}
